*
* CAOS2PRAY CHUNK
*
*# Pray-File "lifePhotographer_pilla.agents"
*# DS-Name "Lifestage Photographer Tool"

*# desc = "This tool creates pictures of your norns when they reach adolescent, adult and old age."
*# Agent Animation File = "lifephotographer_pilla.c16"
*# Agent Sprite First Image = 1
*# Agent Animation Gallery = "lifephotographer_pilla"
*# Web URL = "https://pillazilla.wordpress.com"
*# Web Label = "Pilla's DS Agents"

*# attach lifephotographer_pilla.c16 lifePhotographer_pilla.catalogue
*# depend blnk.c16

**
** Install script
**

**
** Create the Lifestage Photographer button
**

new: simp 1 1 36474 "lifephotographer_pilla" 2 0 9000
flto 10 214
sets name "tooltip" "Photographer is disabled"

**
** Photographer Construction script
**

**
** Lifestage Photographer constructor script
scrp 1 1 36474 10
** Remove other Lifestage Photographer instances
	enum 1 1 36474
		doif targ ne ownr
			kill targ
		endi
	next

	targ ownr
** Set attributes
	attr 38
** Activate!
	mesg writ ownr 0
endm

**
** Lifestage Photographer button click
**

scrp 1 1 36474 1
** Toggle the variable
	doif mame "lifePhotographer" eq 1
		setv mame "lifePhotographer" 0
		sets name "tooltip" "Photographer is disabled"
	else
		setv mame "lifePhotographer" 1
		sets name "tooltip" "Photographer is enabled"
	endi
** Set the pose
	targ ownr
	pose name "lifePhotographer"
endm

**
** Life event script
**

scrp 1 1 36474 127
	doif name "lifePhotographer" eq 1
* make birth photographer, who will
* take a photo after about five seconds
		inst
		setv va00 hist type _p1_ _p2_
		seta va02 mtoc _p1_
		doif va00 = 4 and va02 <> null
			targ va02
			doif cage eq 2 or cage ge 4
				new: simp 1 2 37 "blnk" 0 0 0
				seta ov00 va02
				sets ov01 _p1_
* randomise slightly, so creatures are more
* likely to be in different poses
				tick rand 200 250
			endi
		endi
	endi
endm


**
** Error script
**
scrp 1 1 36474 255
	setv name "Aging Photographer" 0
	pose name "Aging Photographer"
endm

**
** REMOVAL SCRIPT
**

rscr
enum 1 1 36474
	kill targ
next